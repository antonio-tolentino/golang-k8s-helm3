pipeline {
    //Global agent  
    agent none

    environment {
        // Application name
        APP_NAME = "books"
        COMPANY_NAME = "Acme Corporation"

        DEVELOP_GCP_PROJECT = "arquitetura-207620"
        RELEASE_GCP_PROJECT = "arquitetura-207620"
        PROD_GCP_PROJECT = "arquitetura-207620"

        // GCP Secret Manager
        GCP_PROJECT = "arquitetura-207620"
        GCP_SECRET = "books-secrets"
        
        // Image registry
        //REGISTRY = "gcr.io/${GCP_PROJECT}"

        // Namespaces 
        DEVELOP_NAMESPACE = "develop"
        RELEASE_NAMESPACE = "homolog"
        PROD_NAMESPACE = "prod"

        // Environment name
        DEVELOP_ENV = "dev"
        RELEASE_ENV = "hml"
        PROD_ENV = "prd"

        // multiple releases
        MULTIPLE_RELEASES = false

        // Slack notification
        // If you do not intend to use slack notification comment these variables and
        // any variable 'slackHelper' declared in stage 'approval' and 'post actions'
        SLACK_CHANNEL = "#jenkins-test"
        SLACK_CREDENTIAL_ID = "Slack"
        SLACK_DOMAIN = "platformbuilders"

    }

    //keep last 5 builds
    options{
         buildDiscarder(logRotator(numToKeepStr: '5', artifactNumToKeepStr: '5'))
    }
     

    stages {  

        stage('Code: Checkout'){
            agent { node { label 'linux' } }
            steps{
                script{
                    echo "INFO: Checkout code from branch [ ${BRANCH_NAME} ]"
                    checkout scm
                }
            }
        }

        stage('Code: Compile and Test'){
            agent {
                kubernetes {
                    cloud 'local'
                    yamlFile 'pipeline/agents/golang.yml'
                }
            } 
            steps{
                container('golang') { 
                    script {
                        echo "INFO: Generating code coverage reports"
                        sh 'go get -u github.com/gorilla/mux'
                        sh 'go test ./rest-api -coverprofile=coverage.out'
                        sh 'go get -u github.com/jstemmer/go-junit-report'
                        sh 'go test ./rest-api -v -bench -count 5 2>&1 | go-junit-report > junit-report.xml'
                        echo "INFO: Compiling code"
                        sh 'CGO_ENABLED=0 GOOS=linux go build -o books ./...'
                    }                    
                }
            }
            post{
                success{
                    junit '**/*-report.xml'
                    stash name: 'golang-artifacts', includes: "**/coverage.out, **/junit-report.xml, **/books"
                }
            }            
        }

        stage('Code: SonarQube analysis') {
            agent {
                kubernetes {
                    cloud 'local'
                    yamlFile 'pipeline/agents/sonar-scanner-cli.yml'
                }
            }  
            steps{
                container('sonar-scanner-cli') {
                    script{
                        unstash 'golang-artifacts'

                        withSonarQubeEnv('SonarQube') { 
                            sh "/opt/sonar-scanner/bin/sonar-scanner -Dproject.settings=./pipeline/sonar-project.properties"
                        }

                        timeout(time: 5, unit: 'MINUTES') {
                            waitForQualityGate abortPipeline: true
                        }
                    }
                }
            }
        }

        stage('Image: Build and Push'){
            when {
                beforeAgent true
                anyOf {
                    branch "develop"
                    branch "release/*"  
                    branch "master"
                }
            }
            agent {
                kubernetes {
                    cloud 'local'
                    yamlFile 'pipeline/agents/kaniko.yml'
                }
            } 
            environment{
                // Image tag
                IMAGE_TAG = GIT_COMMIT.take(7)
                REGISTRY = "gcr.io/arquitetura-207620"
            }
            steps {
                container('kaniko') {
                    script{
                        
                        // Generate REPOSITORY based on environment and branch
                        def kanikoHelper = load("${WORKSPACE}/pipeline/helpers/kanikoHelper.groovy")
                        REPOSITORY = kanikoHelper.generateRepo()

                        echo "INFO: Building and pushing container image"
                
                        // golang artifacts
                        unstash 'golang-artifacts'

                         sh """
                        /kaniko/executor \
                        --dockerfile=pipeline/Dockerfile \
                        --context=`pwd` \
                        --cache=true \
                        --cache-dir=/workspace/cache \
                        --destination=${REPOSITORY} \
                        --verbosity=debug 
                        """
                    }
                }
            }
        }        

        // Approve notification only for Release and Prod
        stage("Approval notification"){
            agent { node { label 'linux' } }
            when {
                beforeAgent true
                anyOf {
                    branch "release/*"  
                    branch "master"
                }
            }
            steps{
                script{
                    echo 'INFO: Send approval notifications.'
                    // mail
                    def mailHelper = load("${WORKSPACE}/pipeline/helpers/mailHelper.groovy")
                    mailHelper.sendApproval()

                    // slack
                    def slackHelper = load("${WORKSPACE}/pipeline/helpers/slackHelper.groovy")
                    slackHelper.sendApproval()

                }
            }            
        } // end stage approve notification

        // Approve step only for Release and Prod
        stage('Approve deploy?') {
            agent none
            when {
                beforeAgent true
                anyOf {
                    branch "release/*"  
                    branch "master"
                }
            }            
            steps {
                script {
                    timeout(time: 4, unit: 'HOURS') {
                        def answer = input( id: 'deployInput', 
                                            message: 'Deseja aprovar?', 
                                            ok: 'Yes',
                                            submitter: "approver",
                                            submitterParameter: 'SUBMITTER',
                                            parameters: [ booleanParam( defaultValue: true, 
                                                                        description: 'Para aprovar click Yes.', 
                                                                        name: 'autorizado') ])                                            

                        // not confirmed action is aborted.
                        if (!answer['autorizado']){
                            currentBuild.result = 'ABORTED'
                            error("Ação não confirmada! Por favor, Selecione o checkbox para autorizar o deploy.")
                        }   
                    }
                }
             }
        } // end stage approve 

        stage('Kubernetes: Deploy'){
            when {
                beforeAgent true
                anyOf {
                    branch "develop"
                    branch "release/*"  
                    branch "master"
                }
            }
            // AGENT PREFERABLE USAGE
            //agent {
            //    kubernetes {
            //        cloud 'local'
            //        yamlFile 'pipeline/agents/pbdeploy.yml'
            //    }
            //}  
            // USE ONLY IF IMAGE REPOSITORY HAS TO BE FLEXIBLE
            agent {
                kubernetes {
                    cloud 'local'
                    yaml """
                         apiVersion: v1
                         kind: Pod
                         metadata:
                           labels:
                             app: pbdeploy
                           namespace: jenkins
                         spec:
                           serviceAccount: jenkins
                           containers:
                           - name: pbdeploy
                             image: ${REGISTRY}/pbdeploy:v0.3.0
                             env:
                               - name: GOOGLE_APPLICATION_CREDENTIALS
                                 value: /secret/jenkins-gcp-credentials.json  
                             #imagePullPolicy: Always
                             command:
                             - cat
                             tty: true
                             volumeMounts:
                               - name: jenkins-gcp-credentials
                                 mountPath: /secret                               
                           volumes:
                             - name: jenkins-gcp-credentials
                               secret:
                                 secretName: jenkins-gcp-credentials                              
                         """
                }
            }              
            environment{
                // Image tag
                IMAGE_TAG = GIT_COMMIT.take(7)
                REGISTRY = "gcr.io/arquitetura-207620"
            }                      
            steps{
                container('pbdeploy'){
                    script{
                        // Generate ENV_NAME, HELM_NAME, REPOSITORY, NAMESPACE based on defined environment variables
                        def pbdeployHelper = load("${WORKSPACE}/pipeline/helpers/pbdeployHelper.groovy")
                        (ENV_NAME, HELM_NAME, NAMESPACE, REPOSITORY) = pbdeployHelper.genDeployInfo()


                        // Inject secrets from GCP Secret Manager
                        echo "INFO: Inject secrets from GCP Secret Manager to Helm secret template"
                        sh """  gcpsecrets --project=${GCP_PROJECT} \
                                --gcp-secret-name=${GCP_SECRET}\
                                --create-new-secret-file=no \
                                --kube-secret-file=pipeline/helm3/appchart/templates/secrets.yaml
                           """ 

                        // helm 3 
                        echo "INFO: Deploy helm templates"
                        sh """ helm upgrade --install --debug \
                               ${HELM_NAME} \
                               --create-namespace=true \
                               --namespace=${NAMESPACE} \
                               --set image.repository=${REPOSITORY} \
                               --set image.tag=${IMAGE_TAG} \
                               --set nameOverride=${APP_NAME} \
                               -f pipeline/helm3/appchart/values-${ENV_NAME}.yaml \
                               pipeline/helm3/appchart 
                           """

                        echo "INFO: List Helm details"
                        sh "helm list -f ${HELM_NAME} -n ${NAMESPACE}" 

                    }
                }
            }
        }// end stage deploy

    } //end stages
    
    post {
        always {
            node('linux') {
                script{
                    echo 'Commands always executed.'
                }
            }
        }
        success {
            node('linux') {
                script{
                    echo 'INFO: Pipeline finished successifully!'
                    // mail
                    def mailHelper = load("${WORKSPACE}/pipeline/helpers/mailHelper.groovy")
                    mailHelper.sendSuccess()

                    // slack
                    def slackHelper = load("${WORKSPACE}/pipeline/helpers/slackHelper.groovy")
                    slackHelper.sendSuccess()


                }
            }            
        }
        failure {
            node('linux') {
                script{
                    echo 'Error: Pipeline finished with failure!'
                    // mail
                    def mailHelper = load("${WORKSPACE}/pipeline/helpers/mailHelper.groovy")
                    mailHelper.sendFailure()

                    // slack
                    def slackHelper = load("${WORKSPACE}/pipeline/helpers/slackHelper.groovy")
                    slackHelper.sendFailure()

                }
            }            
        }
        cleanup{
            node('linux') {
                script{
                    echo 'Cleaning up workspace...'
                    deleteDir()   
                }
            }
        }
      
    } // end post

} // end pipeline  
